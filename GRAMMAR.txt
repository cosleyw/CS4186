
//Implemented elsewhere
Identifier -> identifier
IntegerConstant -> integer_constant
FloatingConstant -> floating_constant
CharacterConstant -> character_constant
StringLiteral -> string_literal
TypedefName -> typedef_name


ISO c89 39
PrimaryExpression -> Identifier
PrimaryExpression -> IntegerConstant
PrimaryExpression -> FloatingConstant
PrimaryExpression -> CharacterConstant
PrimaryExpression -> StringLiteral
PrimaryExpression -> ( Expression )

ISO c89 39
PostfixExpression -> PostfixExpression [ Expression ]
PostfixExpression -> PostfixExpression ( ArgumentExpressionList )
PostfixExpression -> PostfixExpression ( )
PostfixExpression -> PostfixExpression . Identifier
PostfixExpression -> PostfixExpression -> Identifier
PostfixExpression -> PostfixExpression ++
PostfixExpression -> PostfixExpression --
PostfixExpression -> PrimaryExpression

ArgumentExpressionList -> AssignmentExpression
ArgumentExpressionList -> AssignmentExpression , ArgumentExpressionList

ISO c89 43
UnaryExpression -> PostfixExpression
UnaryExpression -> ++ UnaryExpression
UnaryExpression -> -- UnaryExpression

UnaryExpression -> & UnaryExpression
UnaryExpression -> * UnaryExpression
UnaryExpression -> + UnaryExpression
UnaryExpression -> - UnaryExpression
UnaryExpression -> ~ UnaryExpression
UnaryExpression -> ! UnaryExpression

UnaryExpression -> sizeof UnaryExpression
UnaryExpression -> sizeof ( TypeName )

ISO c89 45
CastExpression -> UnaryExpression
CastExpression -> ( TypeName ) CastExpression

ISO c89 46
MultiplicativeExpression -> MultiplicativeExpression / CastExpression
MultiplicativeExpression -> MultiplicativeExpression % CastExpression
MultiplicativeExpression -> CastExpression
MultiplicativeExpression -> CastExpression * MultiplicativeExpression

ISO c89 46
AdditiveExpression -> AdditiveExpression - MultiplicativeExpression
AdditiveExpression -> MultiplicativeExpression
AdditiveExpression -> MultiplicativeExpression + AdditiveExpression

ISO c89 48
ShiftExpression -> ShiftExpression >> AdditiveExpression
ShiftExpression -> ShiftExpression << AdditiveExpression
ShiftExpression -> AdditiveExpression

ISO c89 48
RelationalExpression -> RelationalExpression < ShiftExpression
RelationalExpression -> RelationalExpression > ShiftExpression
RelationalExpression -> RelationalExpression <= ShiftExpression
RelationalExpression -> RelationalExpression >= ShiftExpression
RelationalExpression -> ShiftExpression

ISO c89 49
EqualityExpression -> RelationalExpression
EqualityExpression -> RelationalExpression == EqualityExpression
EqualityExpression -> RelationalExpression != EqualityExpression

ISO c89 50
AndExpression -> EqualityExpression & AndExpression
AndExpression -> EqualityExpression

ExclusiveOrExpression -> AndExpression
ExclusiveOrExpression -> AndExpression ^ ExclusiveOrExpression

InclusiveOrExpression -> ExclusiveOrExpression
InclusiveOrExpression -> ExclusiveOrExpression | InclusiveOrExpression

ISO c89 51
LogicalAndExpression -> InclusiveOrExpression
LogicalAndExpression -> InclusiveOrExpression && LogicalAndExpression

LogicalOrExpression -> LogicalAndExpression
LogicalOrExpression -> LogicalAndExpression || LogicalOrExpression

ConditionalExpression -> LogicalOrExpression
ConditionalExpression -> LogicalOrExpression ? Expression : ConditionalExpression

ISO c89 53
AssignmentExpression -> ConditionalExpression
AssignmentExpression -> UnaryExpression = AssignmentExpression
AssignmentExpression -> UnaryExpression *= AssignmentExpression
AssignmentExpression -> UnaryExpression /= AssignmentExpression
AssignmentExpression -> UnaryExpression %= AssignmentExpression
AssignmentExpression -> UnaryExpression += AssignmentExpression
AssignmentExpression -> UnaryExpression -= AssignmentExpression
AssignmentExpression -> UnaryExpression <<= AssignmentExpression
AssignmentExpression -> UnaryExpression >>= AssignmentExpression
AssignmentExpression -> UnaryExpression &= AssignmentExpression
AssignmentExpression -> UnaryExpression ^=  AssignmentExpression
AssignmentExpression -> UnaryExpression |= AssignmentExpression

ISO c89 54

Expression -> AssignmentExpression , Expression
Expression -> AssignmentExpression


ISO c89 57
Declaration -> DeclarationSpecifiers ;
Declaration -> DeclarationSpecifiers InitDeclaratorList ;

DeclarationSpecifiers -> StorageClassSpecifier
DeclarationSpecifiers -> StorageClassSpecifier DeclarationSpecifiers
DeclarationSpecifiers -> TypeSpecifier
DeclarationSpecifiers -> TypeSpecifier DeclarationSpecifiers
DeclarationSpecifiers -> TypeQualifier
DeclarationSpecifiers -> TypeQualifier DeclarationSpecifiers

InitDeclaratorList -> InitDeclarator
InitDeclaratorList -> InitDeclarator , InitDeclaratorList

InitDeclarator -> Declarator
InitDeclarator -> Declarator = Initializer

ISO c89 58
StorageClassSpecifier -> typedef
StorageClassSpecifier -> extern
StorageClassSpecifier -> static
StorageClassSpecifier -> auto
StorageClassSpecifier -> register

TypeSpecifier -> void
TypeSpecifier -> char
TypeSpecifier -> short
TypeSpecifier -> int
TypeSpecifier -> long
TypeSpecifier -> float
TypeSpecifier -> double
TypeSpecifier -> signed
TypeSpecifier -> unsigned
TypeSpecifier -> StructOrUnionSpecifier
TypeSpecifier -> EnumSpecifier
TypeSpecifier -> TypedefName


ISO c89 59
StructOrUnionSpecifier -> StructOrUnion Identifier { StructDeclarationList }
StructOrUnionSpecifier -> StructOrUnion { StructDeclarationList }
StructOrUnionSpecifier -> StructOrUnion Identifier

StructOrUnion -> struct
StructOrUnion -> union

StructDeclarationList -> StructDeclaration
StructDeclarationList -> StructDeclaration StructDeclarationList

StructDeclaration -> SpecifierQualifierList StructDeclaratorList ;

SpecifierQualifierList -> TypeSpecifier SpecifierQualifierList
SpecifierQualifierList -> TypeSpecifier
SpecifierQualifierList -> TypeQualifier SpecifierQualifierList
SpecifierQualifierList -> TypeQualifier

StructDeclaratorList -> StructDeclarator
StructDeclaratorList -> StructDeclarator , StructDeclaratorList

StructDeclarator -> Declarator
StructDeclarator -> Declarator : ConditionalExpression
StructDeclarator -> : ConditionalExpression

ISO c89 61
EnumSpecifier -> enum Identifier { EnumeratorList }
EnumSpecifier -> enum { EnumeratorList }
EnumSpecifier -> enum Identifier

EnumeratorList -> Enumerator
EnumeratorList -> Enumerator , EnumeratorList

Enumerator -> Identifier
Enumerator -> Identifier = ConditionalExpression

ISO c89 64
TypeQualifier -> const
TypeQualifier -> volatile

ISO c89 65
Declarator -> Pointer DirectDeclarator
Declarator -> DirectDeclarator

DirectDeclarator -> DirectDeclarator [ ConditionalExpression ]
DirectDeclarator -> DirectDeclarator [ ]
DirectDeclarator -> DirectDeclarator ( ParameterTypeList )
DirectDeclarator -> DirectDeclarator ( IdentifierList )
DirectDeclarator -> DirectDeclarator ( )
DirectDeclarator -> ( Declarator )
DirectDeclarator -> Identifier

Pointer -> *
Pointer -> * TypeQualifierList
Pointer -> * TypeQualifierList Pointer
Pointer -> * Pointer

TypeQualifierList -> TypeQualifier
TypeQualifierList -> TypeQualifier TypeQualifierList 

ParameterTypeList -> ParameterList
ParameterTypeList -> ParameterList , ...

ParameterList -> ParameterDeclaration
ParameterList -> ParameterDeclaration , ParameterList

ParameterDeclaration -> DeclarationSpecifiers Declarator
ParameterDeclaration -> DeclarationSpecifiers AbstractDeclarator
ParameterDeclaration -> DeclarationSpecifiers

IdentifierList -> Identifier
IdentifierList -> Identifier , IdentifierList

ISO c89 69
TypeName -> SpecifierQualifierList AbstractDeclarator
TypeName -> SpecifierQualifierList

AbstractDeclarator -> Pointer
AbstractDeclarator -> Pointer DirectAbstractDeclarator
AbstractDeclarator -> DirectAbstractDeclarator

DirectAbstractDeclarator -> ( AbstractDeclarator )
DirectAbstractDeclarator -> DirectAbstractDeclarator [ ConditionalExpression ]
DirectAbstractDeclarator -> [ ConditionalExpression ]
DirectAbstractDeclarator -> DirectAbstractDeclarator [ ]
DirectAbstractDeclarator -> [ ]
DirectAbstractDeclarator -> DirectAbstractDeclarator ( ParameterTypeList )
DirectAbstractDeclarator -> ( ParameterTypeList )
DirectAbstractDeclarator -> DirectAbstractDeclarator ( )
DirectAbstractDeclarator -> ( )

ISO c89 71
Initializer -> AssignmentExpression
Initializer -> { InitializerList }
Initializer -> { InitializerList , }

InitializerList -> Initializer
InitializerList -> InitializerList , Initializer


ISO c89 75
Statement -> LabeledStatement
Statement -> CompoundStatement
Statement -> ExpressionStatement
Statement -> SelectionStatement
Statement -> IterationStatement
Statement -> JumpStatement

LabeledStatement -> Identifier : Statement
LabeledStatement -> case ConditionalExpression : Statement
LabeledStatement -> default : Statement

CompoundStatement -> { DeclarationList StatementList }
CompoundStatement -> { StatementList }
CompoundStatement -> { DeclarationList }
CompoundStatement -> { }

DeclarationList -> Declaration
DeclarationList -> Declaration DeclarationList

StatementList -> Statement
StatementList -> Statement StatementList

ISO c89 76
ExpressionStatement -> Expression ;
ExpressionStatement -> ;

SelectionStatement -> if ( Expression ) Statement
SelectionStatement -> if ( Expression ) Statement else Statement
SelectionStatement -> switch ( Expression ) Statement

ISO c89 78
IterationStatement -> while ( Expression ) Statement
IterationStatement -> do Statement while ( Expression ) ;
IterationStatement -> for ( Expression ; Expression ; Expression ) Statement
IterationStatement -> for ( ; Expression ; Expression ) Statement
IterationStatement -> for ( Expression ; ; Expression ) Statement
IterationStatement -> for ( ; ; Expression ) Statement
IterationStatement -> for ( Expression ; Expression ; ) Statement
IterationStatement -> for ( ; Expression ; ) Statement
IterationStatement -> for ( Expression ; ; ) Statement
IterationStatement -> for ( ; ; ) Statement

ISO c89 79
JumpStatement -> goto Identifier ;
JumpStatement -> continue ;
JumpStatement -> break ;
JumpStatement -> return Expression ;
JumpStatement -> return ;


ISO c89 81
TranslationUnit -> ExternalDeclaration
TranslationUnit -> ExternalDeclaration TranslationUnit

ExternalDeclaration -> FunctionDefinition
ExternalDeclaration -> Declaration

FunctionDefinition -> DeclarationSpecifiers Declarator DeclarationList CompoundStatement
FunctionDefinition -> Declarator DeclarationList CompoundStatement
FunctionDefinition -> DeclarationSpecifiers Declarator CompoundStatement
FunctionDefinition -> Declarator CompoundStatement

